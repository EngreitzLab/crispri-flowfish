# The main entry point of the workflow.
# After configuring, running snakemake -n in a clone of this repository should successfully execute a dry-run of the workflow.


# this container defines the underlying OS for each job when using the workflow
# with --use-conda --use-singularity
# container: "docker://continuumio/miniconda3"

# report: "report/workflow.rst"

include: "rules/common.smk"
include: "rules/make_count_tables.smk"
include: "rules/estimate_effect_sizes.smk"
include: "rules/convert_clamp_make_windows.smk"
include: "rules/score_peaks.smk"


rule all:
    input: all_input


## Delete this? 
rule create_bowtie2_index:
  input:
    fasta='amplicons/Amplicons.fa'
  output:
    index='amplicons/Amplicons.fa.1.bt2'
  shell:
    """
    bash -c '
      . $HOME/.bashrc 
      conda activate crispresso2_210104
      bowtie2-build {input.fasta} {input.fasta}
    '
    """

# map individual fastqs to the bowtie index
rule map_reads:
    input:
        fastq=lambda wildcards: samplesheet.at[wildcards.SampleID,'fastqR1'],
    output:
        samgzAligned='results/samFiles/{SampleID}.sam.gz',
        fastqUnaligned='results/samFiles/{SampleID}.unaligned.fastq.gz',
        alignment_stats='results/samFiles/{SampleID}.alignment_stats.txt'
    params:
    	index=config['guide_index'],
        fastqUnaligned='results/samFiles/{SampleID}.unaligned.fastq'
    shell:
        """
        zless {input.fastq} | bowtie -v0 -p1 --all -x {params.index} --un {params.fastqUnaligned} - | gzip > {output.samgzAligned}
        ALIGNED_COUNT=$(zcat {output.samgzAligned} | wc -l)
        UNALIGNED_COUNT=$(cat {params.fastqUnaligned} | wc -l)
        UNALIGNED_COUNT=$(( $UNALIGNED_COUNT / 4))
        echo -e "SampleID\tnReadsAligned\tnReadsUnaligned" > {output.alignment_stats}
        echo -e "{wildcards.SampleID}\t$ALIGNED_COUNT\t$UNALIGNED_COUNT" >> {output.alignment_stats}
        gzip {params.fastqUnaligned}
        """

rule aggregate_alignment_stats:
    input:
        lambda wildcards: ["results/samFiles/{SampleID}.alignment_stats.txt".format(SampleID=s) for s in samplesheet['SampleID']]
    output:
        'results/summary/alignment_stats.tsv'
    shell:
        "csvtk concat -t {input} > {output}"


# count reads per guide in each sample
rule count_reads:
    input:
        sam='results/samFiles/{SampleID}.sam.gz'
    output:
        count='results/counts/{SampleID}.count.txt'
    params:
    shell:
        "zless {input.sam} | cut -f3 | sort | uniq -c | tr -s ' ' '\t' | cut -f2-3 > {output.count}"


